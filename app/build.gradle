apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId "com.serwylo.lexica"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 10300
        versionName "1.3.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    lintOptions {
        // The ranslations are crowd-sourced, and so will likely never be 100% complete for all locales.
        disable 'MissingTranslation', 'GoogleAppIndexingWarning'

        // Due to getting the following error with Travis CI:
        //   /home/travis/build/lexica/lexica/app: Error: The SDK platform-tools version ((21)) is too old  to check APIs compiled with API 23; please update [NewApi]
        // Until the newer version of platform-tools is installed on their CI server, lets not consider it a failure.
        abortOnError false

        // Ouptuts report during build, so that it can be seen in the CI log.
        textReport true
    }

    buildTypes {
        all {
            minifyEnabled false
            shrinkResources false
        }
    }

    testOptions {
        // http://tools.android.com/tech-docs/unit-testing-support#TOC-Method-...-not-mocked.-
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/*'
    }
}

dependencies {
    implementation project(':libraries:trie')
    implementation project(':libraries:liblexica')

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    // Used for buttons on the home page and elsewhere, because the default Android ones just aren't
    // quite customizable without having to go into <selectors> and such.
    implementation 'com.github.medyo:fancybuttons:1.9.1'

    testImplementation "junit:junit:4.13"
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'

    androidTestImplementation 'tools.fastlane:screengrab:2.0.0', {
        exclude group: 'androidx.test.uiautomator', module: 'uiautomator'
    }
}

afterEvaluate {
    assembleDebug.dependsOn rootProject.buildDictionaries
    if (tasks.findByPath('assembleRelease')) {
        assembleRelease.dependsOn rootProject.buildDictionaries
    }
}
