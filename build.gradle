// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'java'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}


// NOTE: The below tasks are a little bit hacky. The optimal solution would be to have a nice
// gradle plugin whose source lives in the libraries/ directory, and which the app/ project
// depends on. However, I couldn't figure out how to have a gradle plugin in the same project
// as another gradle build which depended on that plugin.

task buildLanguageAnalysis(type: GradleBuild) {
    buildFile = file('libraries/language-analysis/build.gradle')
    tasks = ['build']
}

task buildTrieBuilder(type: GradleBuild) {
    buildFile = file('libraries/trie-builder/build.gradle')
    tasks = ['build']
}

task buildTrieLib(type: GradleBuild) {
    buildFile = file('libraries/trie/build.gradle')
    tasks = ['build']
}

task buildDictionaries

def languages = [
  "ca",
  "de_DE",
  "en_US",
  "en_GB",
  "es",
  "fa",
  "fr_FR",
  "hu",
  "it",
  "jp",
  "nl",
  "ru",
]
languages.each { lang ->

    def langTask = task "buildDictionary_${lang}"(dependsOn: buildTrieBuilder, type: JavaExec) {
        main = 'com.serwylo.lexica.trie.TrieBuilderApp'
        classpath 'libraries/trie/build/libs/trie.jar', 'libraries/trie-builder/build/libs/trie-builder.jar'
        args = [
                lang,
                file('assets/dictionaries/'),
                file('app/src/main/res/raw/'),
                file('app/src/test/resources/'),
        ]
        outputs.dir("assets/dictionaries/dictionary_${lang}.txt")
    }

    buildDictionaries.dependsOn << langTask

    task "analyseLanguage_${lang}"(dependsOn: [buildLanguageAnalysis, langTask], type: JavaExec) {
        main = 'com.serwylo.lexica.language.LanguageAnalysisApp'
        classpath 'libraries/language-analysis/build/libs/language-analysis.jar'
        args = [
                lang,
                file('app/src/main/res/raw/'),
                file('assets/dictionaries/'),
                file('/tmp/'),
        ]
    }
}
