// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'java'
apply plugin: 'kotlin'

buildscript {
    ext.kotlin_version = '2.1.0'
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.10.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.bjoernq:unmockplugin:0.7.8'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}


// NOTE: The below tasks are a little bit hacky. The optimal solution would be to have a nice
// gradle plugin whose source lives in the libraries/ directory, and which the app/ project
// depends on. However, I couldn't figure out how to have a gradle plugin in the same project
// as another gradle build which depended on that plugin.

tasks.register('buildLanguageAnalysis', GradleBuild) {
    dir = file('libraries/language-analysis')
    tasks = ['shadowJar']
}

tasks.register('buildTrieBuilder', GradleBuild) {
    dir = file('libraries/trie-builder')
    tasks = ['shadowJar']
}

tasks.register('buildTrieLib', GradleBuild) {
    dir = file('libraries/trie')
    tasks = ['build']
}

tasks.register('buildDictionaries')

def languages = [
  "br_no_diacritics",
  "ca",
  "de_DE",
  "de_DE_no_diacritics",
  "en_US",
  "en_GB",
  "es",
  "es_solo_enne",
  "fa",
  "fi",
  "fr_FR",
  "fr_FR_no_diacritics",
  "hu",
  "hr_HR",
  "it",
  "ja",
  "nl",
  "pl",
  "pt_BR",
  "pt_BR_no_diacritics",
  "ru",
  "ru_extended",
  "tr",
  "uk",
]
languages.each { lang ->

    def langTask = task "buildDictionary_${lang}"(dependsOn: buildTrieBuilder, type: JavaExec) {
        main = 'com.serwylo.lexica.trie.TrieBuilderApp'
        classpath 'libraries/trie-builder/build/libs/trie-builder-all.jar'
        args = [
                lang,
                file('assets/dictionaries/'),
                file('app/src/main/res/raw/'),
                file('app/src/test/resources/'),
        ]

        outputs.files([
                "app/src/main/res/raw/words_${lang}.bin",
                "app/src/test/resources/words_${lang}.bin",
	])
    }

    buildDictionaries.dependsOn << langTask

    task "analyseLanguage_${lang}"(dependsOn: [buildLanguageAnalysis, langTask], type: JavaExec) {
        main = 'com.serwylo.lexica.language.LanguageAnalysisApp'
        classpath 'libraries/language-analysis/build/libs/language-analysis-all.jar'
        args = [
                lang,
                file('app/src/main/res/raw/'),
                file('assets/dictionaries/'),
                file('/tmp/'),
        ]
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "21"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "21"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}
